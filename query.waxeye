query <- ws path *(ws :'|' ws path) ws

path <- :'/' | +(?(:'/' ?'/') step)

step <- ?axis_specifier node_test *predicate 
      | abbreviated_step

axis_specifier <- axis_name :'::' 
                | abbreviated_axis_specifier

abbreviated_step <- '.' | '..'

abbreviated_axis_specifier <= ?'@'

axis_name <= 'ancestor'
           | 'ancestor-or-self'
           | 'attribute'
           | 'child'
           | 'descendant'
           | 'descendant-or-self'
           | 'following'
           | 'following-sibling'
           | 'namespace'
           | 'parent'
           | 'preceding'
           | 'preceding-sibling'
           | 'self'

node_test <- node_type ws :'(' ws :')' | name_test

name_test <= '*' | fieldname

node_type <= 'node'

predicate <- :'[' ws predicate_expr ws :']'

predicate_expr <= expr

expr <= or_expr

primary_expr <- :'(' ws expr ws :')' | literal | number | function_call

function_call <- function_name ws :'(' ws ?(argument *(ws :',' ws argument)) ws :')'

function_name <- +[a-zA-Z_] *[a-zA-Z0-9_-]

argument <- expr

or_expr <= and_expr | and_expr ws :'or' ws or_expr

and_expr <= equality_expr | equality_expr ws :'and' ws and_expr

equality_expr <= relational_expr | relational_expr ws eq_op ws equality_expr

relational_expr <= additive_expr | additive_expr ws rel_op ws relational_expr

eq_op <- '=' | '!='

rel_op <- '<' | '<=' | '>' | '>='

additive_expr <= multiplicative_expr | multiplicative_expr ws [+-] ws additive_expr

multiplicative_expr <= unary_expr | unary_expr ws ('*' | ('d' :'iv') | ('m' :'od')) ws multiplicative_expr

unary_expr <= path_expr | '-' unary_expr

path_expr <= filter_expr | filter_expr '/' path | path

filter_expr <= primary_expr

fieldname <- +[a-zA-Z_] *[a-zA-Z0-9_]

literal  <- :'"' *( :'\\' escaped | !'\\' !'"' . ) :'"' | :"'" *( :'\\' escaped | !'\\' !"'" . ) :"'"

escaped <- 'u' [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] | ["/\\bfnrt]

number <- +[0-9] ?('.' +[0-9])

digits <- +[0-9]

ws <: *[ \t\n\r]
